<%args>
	$tourn
	$event
	$chapter
	$debug => undef
</%args>
<%init>
#/
	my $double_entry = $tourn->setting("double_entry");
	my $double_max = $tourn->setting("double_max");
	
	my @clean_students;

	my @students = $chapter->students;

	#Check the students to be sure that they're not entered in an event restricted against this one.

	my $now = DateTime->now;
	my $year = $now->year;
	$year++ if $now->month > 6;

	my $event_double = $event->event_double;

	STUDENT:
	foreach my $student (@students) { 

		#Students who aren't students anymore.
		next STUDENT if ($student->retired);
		next STUDENT if ($student->grad_year < $year && not defined $event->setting("alumni"));

		#Class specific double entry restrictions 
		my @entries = $m->comp("/funclib/student_entries.mas", student => $student, tourn => $tourn);

		#Supplementals are only open to students entered in the tournament
		next STUDENT if $event->setting("supp") &! @entries;

		my %event_double_scoreboard = ();

		foreach my $entry (@entries) { 
			$event_double_scoreboard{$entry->event->event_double->id}++;
		}

		my $entered;

		foreach my $entry (@entries) { 

			# Am I already entered in this event?
			next STUDENT if $entry->event->id == $event->id;

			unless ($entry->dropped || $entry->waitlist) { 

				# Am I already entered in the tournament and in off the waitlist? 
				$entered++;

				# If there's no double entry for the event_double of the other entries, eliminate the student

				# No double entry permitted if you're entered in this event_double
				next STUDENT if $event_double->setting == 2 && $entered;  

				# No double entry permitted if you're already entered and the new event_double forbids double entry
				next STUDENT if $entry->event->event_double->setting == 2;

				#No double entry within the event_double
				next STUDENT if $entry->event->event_double->setting == 1 && ($entry->event->event_double->id == $event_double->id);  

				#No double entry beyond a limit within the event_double
				next STUDENT if $entry->event->event_double->setting == 3 && ($entry->event->event_double->id == $event_double->id && $event_double_scoreboard{$event_double->id} >= $event_double->max); 

			}
		}

		system "/usr/bin/logger Student ".$student->id." is entered in $entered events" if $debug;

		#Unlimited double entry
		push (@clean_students, $student) if $double_entry eq "unlimited";
		next STUDENT if $double_entry eq "unlimited";

		#No double entry
		next STUDENT if ($entered && $double_entry eq "none"); 

		#One event double entry
		next STUDENT if ($double_entry eq "max_events" && $entered >= $double_max);
	
		push (@clean_students, $student);
	}	

	return @clean_students;
	
</%init>
