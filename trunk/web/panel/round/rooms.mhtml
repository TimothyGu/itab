<%args>
	$account
	$round_id
	$return => undef
</%args>
<%init>

	my $round = Tab::Round->retrieve($round_id);
	my $round_type = $round->type;

	my $text = $account->first." ".$account->last." (".$account->email.") has repaneled the rooms in ".$round->realname." of ".$round->event->abbr;

	Tab::TournChange->create({ 
		type => 'tabbing',
		event => $round->event->id,
		tourn => $round->event->tourn->id,
		account => $account->id,
		text => $text
	});

	my @panels = $m->comp('/funclib/round_panels_ada.mas', round => $round);
	my @rooms = $m->comp("/funclib/clean_rooms.mas", round => $round);

	my %judge_already = ();
	my %judge_reserved = ();

	my %room_count = ();
	my @undone;
	my $flights = $round->flighted;

	my %judge_count;
	my %panel_judges;
	my %panel_entries;

	my %panel_judge_count;
	my %panel_ada;

	my $max_judges;

	foreach my $panel (@panels) { 

		next if $panel->room > 0;
		next if $panel->bye > 0;

		@{$panel_judges{$panel->id}} = $m->comp("/funclib/panel_judges.mas", panel => $panel);
		@{$panel_entries{$panel->id}} = $m->comp("/funclib/panel_entries.mas", panel => $panel);

		$max_judges = scalar @{$panel_judges{$panel->id}} if scalar @{$panel_judges{$panel->id}} > $max_judges;

		foreach my $judge (@{$panel_judges{$panel->id}}){ 
			$judge_count{$judge->id}++;
			$panel_judge_count{$panel->id} += $judge_count{$judge->id}++;
			$panel_ada{$panel->id}++ if $judge->ada;
			$judge_reserved{$judge->id} = $judge->setting("room_reserved") unless $round_type eq "elim" || $round_type eq "final";
		}

		foreach my $entry (@{$panel_entries{$panel->id}}) { 
			$panel_ada{$panel->id}++ if $entry->ada;
		}

	}

	@panels = sort {$a->letter <=> $b->letter} @panels;
	@panels = sort {$panel_judge_count{$b->id} <=> $panel_judge_count{$a->id}} @panels;
	@panels = sort {$panel_ada{$b->id} <=> $panel_ada{$a->id}} @panels;

	my %room_used;

	foreach my $panel (@panels) { 

		next if $panel->bye > 0;
		next if $panel->room > 0;

		my $flight = $panel->flight;

		my $room;

		my @judges = @{$panel_judges{$panel->id}} if $panel_judges{$panel->id};

		foreach my $judge (@judges) { 
			if ($judge_reserved{$judge->id}) { 
				$room = Tab::Room->retrieve($judge_reserved{$judge->id}); 
			} else { 
				$room = $judge_already{$judge->id} unless $room; 
			}
		}

		my %used;

		undef $room if $room_used{$room."-".$flight};

		if ($panel_ada{$panel}) { 

			foreach my $other_room (@rooms) { 
				next if $used{$other_room}++;
				$room = $other_room if $other_room->ada;
				last if $room;
			}
		}

		unless ($room) { 
			foreach my $other_room (@rooms) { 
				next if $used{$other_room}++;
				$room = $other_room;
				last if $room;
			}
		}

		if ($room && $room->id) { 
			$panel->room($room->id) if $room;
			$panel->update;
			$room_count{$room->id}++;
			foreach my $judge (@judges) { 
				$judge_already{$judge->id} = $room;
			}
			$room_used{$room."-".$flight}++;
		} else { 
			push @undone, $panel;
		}
	}

	if ($max_judges == 1) { 

		PANEL:
		foreach my $panel (@undone) { 

			foreach my $room (@rooms) { 
				next if $room_count{$room->id} >= $flights;
				$panel->room($room->id);
				$room_count{$room->id}++;
				$panel->flight($room_count{$room->id});
				$panel->update;
				next PANEL;
			}
		}

	}

	my $msg = "Rooms have been assigned";
	$m->redirect("/panel/schemat/show.mhtml?round_id=$round_id&disp=1&msg=$msg");

</%init>
